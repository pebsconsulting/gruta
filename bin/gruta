#!/usr/bin/perl

use strict;
use warnings;

use File::Temp;

use Gruta;
use Gruta::Source::DBI;
use Gruta::Source::FS;

my $cmd = arg();
my $src_str = arg();

my $src = new_source($src_str);
my $g = Gruta->new( sources => $src );

if ($cmd eq 'copy') {
	my $new_src = arg();
	my $dst = new_source( $new_src );

	$dst->create();

	$g->transfer_to_source( $dst );
}
elsif ($cmd eq 'topics') {
	foreach my $t ($g->topics()) {
		print $t, "\n";
	}
}
elsif ($cmd eq 'topic') {
	my $topic_id = arg();

	my $topic = $g->topic($topic_id);

	foreach my $f ($topic->afields()) {
		print $f, ': ', ($topic->get($f) || ''), "\n";
	}
}
elsif ($cmd eq 'stories') {
	my $topic_id = arg();

	foreach my $s ($g->stories($topic_id)) {
		print $s, "\n";
	}
}
elsif ($cmd eq 'story') {
	my $topic_id	= arg();
	my $id		= arg();

	print get_story($topic_id, $id);
}
elsif ($cmd eq 'edit_story') {
	my $topic_id	= arg();
	my $id		= arg();

	my $fh = File::Temp->new();
	print $fh get_story($topic_id, $id);
	my $fn = $fh->filename();
	$fh->close();

	my $mtime = (stat($fn))[9];
	system('$EDITOR ' . $fn);

	if ($mtime != (stat($fn))[9]) {
		print "Changed!\n";
	}
}
elsif ($cmd eq 'create') {
	# do nothing
	1;
}
elsif ($cmd eq 'tags') {
	foreach my $t ($g->tags()) {
		print join(', ', @{$t}), "\n";
	}
}
else {
	usage();
}

exit 0;


sub arg
{
	my $r = shift(@ARGV) or usage();
	return $r;
}


sub usage
{
	print "Usage: $0 {cmd} {source} [args]\n";

	exit 1;
}

sub new_source
{
	my $src_str = shift;
	my $src;

	if ($src_str =~ /^dbi:/) {
		$src = Gruta::Source::DBI->new( string => $src_str );
	}
	else {
		$src = Gruta::Source::FS->new( path => $src_str );
	}

	return $src;
}

sub get_story
{
	my $topic_id	= shift;
	my $id		= shift;
	my @r		= ();

	my $story = $g->story($topic_id, $id);

	foreach my $f ($story->afields()) {
		if ($f ne 'content') {
			push (@r, $f . ': ' . ($story->get($f) || ''));
		}
	}

	push(@r, 'tags: ' . join(', ', $story->tags()));
	push(@r, '');
	push(@r, $story->get('content'));
	push(@r, '');

	return join("\n", @r);
}
